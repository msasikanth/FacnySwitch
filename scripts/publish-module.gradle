apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  from android.sourceSets.main.java.srcDirs
  from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
  archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId PUBLISH_GROUP_ID
        artifactId PUBLISH_ARTIFACT_ID
        version PUBLISH_VERSION

        from components.release

        artifact androidSourcesJar

        pom {
          name = PUBLISH_ARTIFACT_ID
          description = 'Fancy Switch'
          url = 'https://github.com/msasikanth/fancy-switch'
          licenses {
            license {
              name = 'Fancy Switch License'
              url = 'https://github.com/msasikanth/fancy-switch/blob/main/LICENSE.md'
            }
          }
          developers {
            developer {
              id = 'sasikanth'
              name = 'Sasikanth Miriyampalli'
              email = 'hello@sasikanth.dev'
            }
          }

          scm {
            connection = 'scm:git:github.com/msasikanth/fancy-switch.git'
            developerConnection = 'scm:git:ssh://github.com/msasikanth/fancy-switch.git'
            url = 'https://github.com/msasikanth/fancy-switch/tree/main'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(
      rootProject.ext["signing.keyId"],
      rootProject.ext["signing.key"],
      rootProject.ext["signing.password"],
  )
  sign publishing.publications
}
